#!/bin/zsh -f

# My ZSH configuration which I try to keep as minimal and
# fast as possible.

# Use Emacs key bindings, ZSH will default this to
# Vim if the default editor is Vim.
bindkey -e

# Extend the path
typeset -U PATH path

# Environment
export LANG='en_US.UTF-8'
export LC_ALL='en_US.UTF-8'
export XDG_DATA_HOME=$HOME/.local/share
export EDITOR="nvim"

# Mac-specific stuff
if [[ "$(uname)" == "Darwin" ]]; then
  source ~/.zshrc.mac
fi

# Local path
path+=("$HOME/.local/bin")

# Neovim
path+=("/opt/nvim/bin")

# Erlang
export KERL_CONFIGURE_OPTIONS="--disable-debug --without-javac"

# Rust
path+=("$HOME/.cargo/bin")

# Zig
path+=("$HOME/.zvm/self")
path+=("$HOME/.zvm/bin")

export PATH

# Shortcuts
alias tt='tmux new-session -A -s main'

if (( $+commands[bat] ))
then
  alias cat='bat'
fi

# Magit on the commandline
if (( $+commands[gitu] ))
then
  alias gu='gitu'
fi

# Replace `ls` with `eza`, if installed
if (( $+commands[eza] ))
then
  alias ls='eza'                              # ls
  alias l='eza -lbF --git'                    # list, size, type, git
  alias ll='eza -lbGF --git'                  # long list
  alias llm='eza -lbGd --git --sort=modified' # long list, modified date sort
fi

# Some useful Git shortcuts
alias gc='git commit'
alias gs='git status --ignore-submodules=dirty'
alias gp='git push origin HEAD'
alias gf='git pull origin HEAD'

# Mise version manager
if (( $+commands[mise] ))
then
  eval "$(~/.local/bin/mise activate zsh)"
fi

# Setup environment variables per directory.
if (( $+commands[direnv] ))
then
  eval "$(direnv hook zsh)"
fi

# Quickly jump to directories with the help of Z
if [ -d "$HOME/.zsh/zsh-z" ]
then
  source $HOME/.zsh/zsh-z/zsh-z.plugin.zsh
fi

# Fish like auto-suggestions
if [ -d "$HOME/.zsh/zsh-autosuggestions" ]
then
  source ~/.zsh/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

# Fish like highlighting
if [ -d "$HOME/.zsh/zsh-syntax-highlighting" ]
then
  source ~/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

# Automatically close, delete and skip matching delimiting pairs.
if [ -d "$HOME/.zsh/zsh-autopair" ]
then
  source ~/.zsh/zsh-autopair/autopair.zsh
  autopair-init
fi

# Nice menu for auto-complete, must be loaded after
# the Z plugin.
zstyle ':completion:*' menu select

# Check if EAT_SHELL_INTEGRATION_DIR exists and is a directory before sourcing
if [ -n "$EAT_SHELL_INTEGRATION_DIR" ] && [ -d "$EAT_SHELL_INTEGRATION_DIR" ]
then
  source "$EAT_SHELL_INTEGRATION_DIR/zsh"
fi

# Sync commandline history between sessions.
if (( $+commands[atuin] ))
then
  eval "$(atuin init zsh)"
fi

# Setup SSH agent on remote server.
if [[ "$(hostname)" == *"bytebottom.com"* ]] || [[ "$HOST" == *"bytebottom.com"* ]]; then
    ssh_env_file="$HOME/.ssh/ssh-agent-env"

    function ssh_agent_is_running() {
        [[ $(pgrep -u $USER ssh-agent | wc -l) -gt 0 ]]
    }

    function start_ssh_agent() {
        (umask 066; ssh-agent > $ssh_env_file)
        source $ssh_env_file > /dev/null
    }

    function kill_ssh_agent() {
        [[ -n "$SSH_AGENT_PID" ]] && ssh-agent -k > /dev/null 2>&1
        rm -f $ssh_env_file
    }

    if ssh_agent_is_running; then
        source $ssh_env_file > /dev/null
    else
        start_ssh_agent
    fi

    if [ -z "$TMUX" ]; then
        # Add my key if it's not added yet, we only do it outside of tmux
        # because I don't want the check to run for every new window in tmux.
        ssh-add -l &>/dev/null || ssh-add ~/.ssh/id_rsa &>/dev/null

        # When I quit the server, also kill the agent.
        trap 'kill_ssh_agent' EXIT
    fi
fi

# Enable autocompletion
autoload -Uz compinit; compinit

# The pure prompt
fpath+=($HOME/.zsh/pure)
autoload -U promptinit; promptinit
prompt pure
